repositories {
    mavenCentral()
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'findbugs'

    group = 'org.microost.poco'
    version = '0.1.0-SNAPSHOT'
    description = 'Plain Old Configuration Objects'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    }

    jar {
        manifest {
            attributes 'Implementation-Title': project.name,
                       'Implementation-Version': project.version,
                       'Implementation-Vendor-Id': project.group,
                       'Specification-Title': project.name,
                       'Specification-Version': project.version
        }
    }

    javadoc {
        options {
            locale = 'en_US'
            encoding = 'UTF-8'
        }
    }

    task testsJar(type: Jar, dependsOn: classes) {
        classifier = 'tests'
        from sourceSets.test.output
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives testsJar
        archives sourcesJar
        archives javadocJar
    }

    tasks.withType(Checkstyle) {
        reports {
            // Not to skip up-to-date checkstyles.
            outputs.upToDateWhen { false }
        }
    }

    checkstyle {
        toolVersion '8.7'
        configFile = file("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
        ignoreFailures = false
        maxWarnings = 0  // https://github.com/gradle/gradle/issues/881
    }

    findbugs {
        ignoreFailures = false
    }
}
